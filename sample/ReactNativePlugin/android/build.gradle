// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "29.0.2"
        minSdkVersion = 16
        compileSdkVersion = 29
        targetSdkVersion = 29
        supportLibVersion = "28.0.0"

        /* If SHAPSHOT enabled and local repository available */
        useLocalSnapshot = Boolean.parseBoolean("${project.USE_LOCAL_SNAPSHOT}") &&
            file("../../../.maven-local").exists()
    }
    repositories {
        if (useLocalSnapshot) {
            maven { url uri("../../../.maven-local") }
        }
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("com.android.tools.build:gradle:3.5.1")

        if (useLocalSnapshot) {
            classpath "com.klarna:plugin:+"
        } else {
            classpath "gradle.plugin.com.klarna:plugin:+"
        }
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        google()
        jcenter()
    }
}

/* DIAGNOSTIC CODE */
logger.info("allow use local maven snapshot: ${useLocalSnapshot}")
logger.info("found '../../../.maven-local' : ${file("../../../.maven-local").exists()}")